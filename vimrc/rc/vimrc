" OBARA Taihe's vimrc
" Copyright (C) 2017 OBARA Taihei
"=============================================================================

" Vim directories
let $CONFIG_PATH = expand('~/.config')
let $VIM_PATH = fnamemodify(resolve(expand('<sfile>:p')), ':h:h')
let $VIM_RC_PATH = $VIM_PATH . '/rc'
let $VIM_CONFIGS_PATH = $VIM_PATH . '/configs'
let $VIM_FILES = expand('~/vimfiles')
let $VIM_BACKUP = $VIM_FILES . '/backup'
let $VIM_SESSIONS = $VIM_FILES . '/sessions'
let $VIM_SWAP = $VIM_FILES . '/swap'
let $VIM_UNDO = $VIM_FILES . '/swap'

function! s:source(path) abort
  let l:abspath = resolve($VIM_RC_PATH . '/'. a:path)
  execute 'source' fnameescape(l:abspath)
endfunction

function! s:load_env(path) abort
  let l:path = expand(a:path)
  if !filereadable(l:path)
    return
  endif
  for line in readfile(l:path)
    let l:kv = split(line, '=')
    call execute('let $'. l:kv[0] . ' = ' . '"' . l:kv[1] . '"')
  endfor
endfunction

function! s:load_local_plugins(path) abort
  let l:plugins_path = fnamemodify(a:path, ':p')
  if !isdirectory(l:plugins_path)
    return
  endif

  let l:paths = split(glob(l:plugins_path . '*'), '\n')
  if empty(l:paths)
    return
  endif

  for l:path in l:paths
    let l:name = fnamemodify(l:path, ':t')
    if l:name ==# 'nvim'
      if has('nvim')
        call s:load_local_plugins(l:path)
      endif
    elseif l:name ==# 'vim'
      if !has('nvim')
        call s:load_local_plugins(l:path)
      endif
    else
      if isdirectory(l:path)
        execute 'set runtimepath+=' . l:path
      endif
    endif
  endfor
endfunction

if has('vim_starting')
  call s:load_env($VIM_PATH . '/.env')
  call s:source('init.vim')
endif

" Load local plugins
let s:local_plugin_dir = $VIM_PATH . '/lplugins'
call s:load_local_plugins(s:local_plugin_dir)

call s:source('options.vim')
call s:source('commands.vim')
call s:source('mappings.vim')

if has('gui_running')
  call s:source('gui.vim')
endif

if core#is_windows()
  call s:source('windows.vim')
endif

" Required
filetype plugin indent on
syntax enable
filetype detect

set background=dark
colorscheme obaland-solarized

set secure

" vim:set ft=vim sw=2 sts=2 et:
